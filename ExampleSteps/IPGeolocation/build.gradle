buildscript {
    // build script plugins and dependencies for root project

    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:5.1.0'
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '5.1.0'
}

dependencies {
    implementation(
            'commons-io:commons-io:2.6',
            "org.json:json:20160810",
            "org.apache.commons:commons-lang3:3.9",
            "org.apache.logging.log4j:log4j-core:2.12.0",
            "org.apache.logging.log4j:log4j-api:2.12.0"
    )
}

/*
Package the step into a jar together with its dependencies.
Relocate the dependencies to ensure dependencies version doesn't conflict with data studio
*/
shadowJar {
    //minimize() - DO NOT USE minimize() because classes initialized through reflections or service loader pattern will be excluded
    zip64 = true
    relocate 'org.apache.commons.io', 'example.org.apache.commons.io'
    relocate 'org.apache.http', 'example.org.apache.http'
    relocate 'org.json', 'example.org.json'
}

tasks.test.dependsOn tasks.shadowJar
jar.enabled = false
assemble.dependsOn(shadowJar)

task copyToAddons(type: Copy) {
    from "$buildDir/libs"
    include "**/*.jar"
    into "../../addons"
    dependsOn(shadowJar)
}